<!DOCTYPE html>
<html lang="ja">
<head>
<meta charset="UTF-8">
<link rel="stylesheet" type="text/css" href="../default.css">
<script type="text/javascript">
function storage(id){ obj=(document.all)?document.all(id):((document.getElementById)?document.getElementById(id):null); if(obj) obj.style.display=(obj.style.display=="none")?"block":"none"; }
</script>
<script type="text/javascript" src="https://cdnjs.cloudflare.com/ajax/libs/mathjax/2.7.0/MathJax.js?config=TeX-AMS_CHTML">
</script>
<title>MathematicaとMapleの覚え書き/松浦望</title>
</head>
<body>
Mathematicaの覚え書き
<ul>
<!----------------------------------------------------------------------------->
<li><a href="./mathematica.pdf">Mathematica練習帳</a>
<li><a href="#list" onclick="storage('list');return false">リストの操作あれこれ</a>
<div id="list" style="display:none">
<pre>
入力 Total[{a,b,c}]
出力 a+b+c

入力 Accumulate[{a,b,c}]
出力 {a,a+b,a+b+c}

入力 RotateLeft[{a,b,c}]
出力 {b,c,a}

入力 RotateRight[{a,b,c}]
出力 {c,a,b}

入力 {a,b,c}+x
出力 {a+x,b+x,c+x}

入力 {a,b,c}*x
出力 {a*x,b*x,c*x}

入力 {a,b,c}+{x,y,z}
出力 {a+x,b+y,c+z}

入力 {a,b,c}*{x,y,z}
出力 {a*x,b*y,c*z}

入力 {{a,b,c},{d,e,f}}*{{x,y,z},{u,v,w}}
出力 {{a*x,b*y,c*z},{d*u,e*v,f*w}}

入力 Map[Total,{{a,b,c},{d,e,f}}*{{x,y,z},{u,v,w}}]
出力 {a*x+b*y+c*z,d*u+e*v+f*w}

入力 Map[f,{a,b,c}]
出力 {f[a],f[b],f[c]}

入力 Through[{f,g,h}[a]]
出力 {f[a],g[a],h[a]}

入力 Thread[f[{a,b,c},x]]
出力 {f[a,x],f[b,x],f[c,x]}

入力 Thread[f[x,{a,b,c}]]
出力 {f[x,a],f[x,b],f[x,c]}

入力 Thread[f[{a,b,c},{x,y,z}]]
出力 {f[a,x],f[b,y],f[c,z]}

入力 Map[Through,Thread[{f,g}[{a,b,c}]]]
出力 {{f[a],g[a]},{f[b],g[b]},{f[c],g[c]}}

入力 Thread[{x,y,z}=={a,b,c}]
出力 {x==a,y==b,z==c}

入力 Operate[g,f[a,b]]
出力 g[f][a,b]
</pre>
</div>
<!----------------------------------------------------------------------------->
<li><a href="#hirota" onclick="storage('hirota');return false">広田微分</a>
<div id="hirota" style="display:none">
広田の \(D\)-operator は2変数関数の組 \(f, g\) に対して
\[
D_x^m D_y^n\, f\cdot g =
\left.
\left(\frac{\partial}{\partial x} - \frac{\partial}{\partial x'}\right)^m
\left(\frac{\partial}{\partial y} - \frac{\partial}{\partial y'}\right)^n
f \left(x, y\right)
g \left(x', y'\right)
\right|_{x'=x,\, y'=y}
\]
と定義される偏微分作用素である。関数 \(f, g\) が3変数以上の場合も同じように定める。広田微分を Mathematica で計算するには、たとえば再帰的に
<pre>
HD[listofvars_,listofnums_][f_,g_]:= Module[{var,num,max,index,currentvar},
    var=listofvars;
    num=listofnums;
    max=Length[var];
    index=1;
    currentvar=var[[index]];
    If[max!=Length[num],Return[],If[num==Table[0,max],Return[f*g],
        While[index<=max,
            (*starting main part*)
            While[num[[index]]>0,
                num=ReplacePart[num,index->num[[index]]-1];
                Return[HD[var,num][D[f,currentvar],g]-HD[var,num][f,D[g,currentvar]]]];
            (*ending main part*)
            index++;
            currentvar=var[[index]];]]]];
</pre>
と書けばよい。使い方は、もし \(D_x^2\, f\cdot g\) を計算したければ
<pre>
HD[{x},{2}][f[x],g[x]]
</pre>
とし、あるいはもし \(D_t D_x^2\, f\cdot g\) を計算したければ
<pre>
HD[{x,t},{2,1}][f[x,t],g[x,t]]
</pre>
とする。
</div>
<!----------------------------------------------------------------------------->
<li><a href="#setdelayed" onclick="storage('setdelayed');return false">即時定義と遅延定義</a>
<div id="setdelayed" style="display:none">
<ol>
<li>
即時定義 <pre style="display:inline">=</pre> と遅延定義 <pre style="display:inline">:=</pre> でたとえば
<pre>
a[x_]=D[x^2,x];
b[x_]:=D[x^2,x];
</pre>
と書いたとき <pre style="display:inline">a[2]</pre> の値は正しく <pre style="display:inline">4</pre> と表示されるが <pre style="display:inline">b[2]</pre> の評価はエラーになる。後者を評価したい場合は <pre style="display:inline">b[x]/.x->2</pre> と書く。一般に、関数を定義するときは、もしそれが即時定義で書けるのなら即時定義で書いておくほうが都合のよいことが多い。
<li>
ただし、遅延定義のほうが好都合のこともある。たとえば、平面内の2点の座標を与えてその垂直二等分線の方程式を返すような関数は、遅延定義で
<pre>
line[pt1_,pt2_]:=Module[{midpt,diff},
    midpt=1/2*(pt1+pt2);
    diff=pt2-pt1;
    diff[[2]]*(y-midpt[[2]])==-diff[[1]]*(x-midpt[[1]])
]
</pre>
と書ける。もしこれを即時定義で
<pre>
line[pt1_,pt2_]=Module[{midpt,diff},
    midpt=1/2*(pt1+pt2);
    diff=pt2-pt1;
    diff[[2]]*(y-midpt[[2]])==-diff[[1]]*(x-midpt[[1]])
]
</pre>
書いてしまうと、期待通りの出力が得られない。なぜなら、この書き方では引数<pre style="display:inline">pt1</pre>, <pre style="display:inline">pt2</pre>をベクトルだと宣言していないため<pre style="display:inline">midpt[[1]]</pre>, <pre style="display:inline">midpt[[2]]</pre>, <pre style="display:inline">diff[[1]]</pre>, <pre style="display:inline">diff[[2]]</pre>がまったく異なる意味に解釈されてしまうため。
<li>
また一般に、もし遅延定義をした場合は、関数の中身にあらかじめ記号的に評価できるような部分があれば <pre style="display:inline">Evaluate</pre> しておくとよい。たとえば
<pre>
f[x_]:=Integrate[Exp[-t^2],{t,0,x}];
g[x_]:=Evaluate[Integrate[Exp[-t^2],{t,0,x}]];
{Timing[Do[f[k],{k,0,100}]],Timing[Do[g[k],{k,0,100}]]}
</pre>
の実行結果は <pre style="display:inline">{{0.505576,Null},{0.000298,Null}}</pre> などとなり、このように実行時間にかなりの差が生じうる。
</ol>
</div>
<!----------------------------------------------------------------------------->
<li><a href="#nintegrate" onclick="storage('nintegrate');return false">数値積分あれこれ</a>
<div id="nintegrate" style="display:none">
<ol>
<li>
<pre style="display:inline">NIntegrate</pre> を使うとき、評価の順番が大切になることがある。たとえば
<pre>
f[a_]:=NIntegrate[(2-a)*Sin[a*x],{x,0,Pi}]
</pre>
と書くと <pre style="display:inline">f[0.5]</pre> は計算できるが <pre style="display:inline">f[a]</pre> はエラーとなる。したがって例えば <pre style="display:inline">NMaximize[f[a],a]</pre> のように関数 <pre style="display:inline">f</pre> を数値的に最大化することができなくなってしまう。計算の順序をコントロールするには、<pre style="display:inline">NumericQ</pre> とパターンのテストを使って、関数 <pre style="display:inline">f</pre> が数値を受けとった場合にのみこれを評価するように定義する。すなわち
<pre>
f[a_?NumericQ]:=NIntegrate[(2-a)*Sin[a*x],{x,0,Pi}]
</pre>
とする。このとき <pre style="display:inline">f[a]</pre> を評価すると関数が未評価のまま返されるので、したがって例えば <pre style="display:inline">NMaximize[f[a],a]</pre> のように数値的に最大化することができるようになる。
<li>
<pre style="display:inline">NIntegrate</pre> の評価を高速化する方法は、被積分関数の性質による。
一般には <pre style="display:inline">NIntegrate</pre> のオプション <pre style="display:inline">"SymbolicProcessing"</pre> を <pre style="display:inline">0</pre> に設定し、積分を記号的に操作しないようにすると高速に評価できる場合がある。
<pre>
NIntegrate[f[x],{x,0,1},Method->{Automatic,"SymbolicProcessing"->0}]
</pre>
あるいはもし被積分関数に特異点があれば <pre style="display:inline">Exclusions</pre> オプションで教えておくとよい。
<pre>
NIntegrate[1/Sqrt[Sin[x]],{x,0,10},Exclusions->Sin[x]==0]
</pre>
<li>
具体例を挙げる。関数 \(\kappa\) を好きなようにひとつ与えて、それを曲率に持つような速さ \(1\) の平面曲線 \(\gamma\) を描くことを考えよう。そのような曲線 \(\gamma\) は
\[
\gamma \left(s\right) = \int_0^s
\begin{bmatrix}
\cos \theta \left(\sigma\right)\\
\sin \theta \left(\sigma\right)
\end{bmatrix}
d\sigma,\quad
\theta \left(s\right) = \int_0^s \kappa \left(\sigma\right) d\sigma
\]
と表現できるから、大体 \(\kappa\) を2回ほど数値積分すれば \(\gamma\) が描画できる。
たとえば
<pre>
kappa[s_]=Sin[Cos[s]];
init=0;
theta[s_?NumericQ]:=NIntegrate[kappa[ss],{ss,init,s},Method->{Automatic,"SymbolicProcessing"->0}];
gamma[s_?NumericQ]:=Module[{xcoord,ycoord},
    xcoord=NIntegrate[Cos[theta[ss]],{ss,init,s},Method->{Automatic,"SymbolicProcessing"->0}];
    ycoord=NIntegrate[Sin[theta[ss]],{ss,init,s},Method->{Automatic,"SymbolicProcessing"->0}];
    {xcoord,ycoord}];
ParametricPlot[gamma[s],{s,init,2*Pi}]
</pre>
と書けばよい。
</ol>
</div>
<!----------------------------------------------------------------------------->
<li><a href="#boldfigure" onclick="storage('boldfigure');return false">テストの問題用紙に載せるグラフィクス</a>
<div id="boldfigure" style="display:none">
テストの問題用紙に載せるグラフィクスを生成するときは、各種 Plot コマンドのオプションとして
<pre>
PlotPoints->100,
PlotRange->{{-5,5},{-5,5}},
Ticks->{Table[n,{n,-5,5}],Table[n,{n,-5,5}]},
Axes->True,
AxesOrigin->{0,0},
PlotStyle->Black,
AxesStyle->Black,
PlotLabel->None,
LabelStyle->{GrayLevel[0],Bold}
</pre>
などを付ける。原稿を実際に印刷して、とくに座標軸や目盛りが濃くはっきりと印字できているか確認すること。
</div>
<!----------------------------------------------------------------------------->
<!--
<li><a href="#xxx" onclick="storage('xxx');return false">xxx</a>
<div id="xxx" style="display:none">
テスト
<pre>
Plot
</pre>
など
</div>
-->
<!----------------------------------------------------------------------------->
</ul>
Mapleの覚え書き
<ul>
<!----------------------------------------------------------------------------->
<li><a href="#dsolve-numeric" onclick="storage('dsolve-numeric');return false">数値積分あれこれ</a>
<div id="dsolve-numeric" style="display:none">
<ol>
<li>
<pre style="display:inline">dsolve</pre> を使うとき、
</ol>
</div>
<!----------------------------------------------------------------------------->
<!--
<li><a href="#xxx" onclick="storage('xxx');return false">xxx</a>
<div id="xxx" style="display:none">
テスト
<pre>
Plot
</pre>
など
</div>
-->
<!----------------------------------------------------------------------------->
</ul>
</body>
</html>
